; -*- mode: clojure; -*-
; vim: filetype=clojure

(logging/init {:file "riemann.log"})

; Listen on the local interface over TCP (5555), UDP (5555), and websockets
; (5556)
(let [host "127.0.0.1"]
  (tcp-server {:host host})
  (udp-server {:host host})
  (ws-server  {:host host}))

; Expire old events from the index every 5 seconds.
(periodically-expire 5)

(repl-server {:host "127.0.0.1"})

(defn consumer-service? [event]
  (when-let [service-name (:service event)]
    (re-matches #".*consumer.*" service-name)))

(defn disk-free-low? [limit event]
  (and (= "percent_bytes" (:type event))
       (= "free" (:type_instance event))
       (< (:metric event) limit)))

(defn df-body [free-space-limit [e :as events]]
  (if (= 1 (count events))
    (str "free space "
         (format "%.02f" (:metric e))
         " below "
         free-space-limit
         "% on host"
         (:host e)
         " in mount point "
         (:plugin_instance e))
    (str "Check free space on hosts "
         (clojure.string/join "," (set (map :host events)))
         " on mount points "
         (clojure.string/join "," (set (map :plugin_instance events))))))

(defn rate-body [rate-limit [e :as events]]
  (if (= (count events) 1)
    (str "Rate "
         (format "%d/s" (:metric e))
         " below limit "
         (format "%d/s" rate-limit)
         " for service '" (:service e) "'")
    (str "Too low messages rate for the last two minutes for "
         (count events) " events"
         (clojure.string/join \newline (for [event events]
                                         (str (:service event) " at rate " (:metric event)))))))

(let [index (index)
      email (mailer {:from "riemann@tiuhti.net"})
      free-space-limit 10
      rate-limit 3
      df-email (mailer {:from "riemann@tiuhti.net"}
                       {:body (partial df-body free-space-limit)})
      rate-email (mailer {:from "riemann@tiuhti.net"}
                         {:body (partial rate-body rate-limit)})]
  (streams
    (default :ttl 60
      index

      #_(info "event" %)

      (expired
        (fn [event]
          (info "expired" event)))

      (where (disk-free-low? free-space-limit event)
        (with :state "fail"
          (rollup 3 120
            (df-email "kimmo@tiuhti.net"))))

      (where (tagged-any "rate")
        (where (and (service #".*messages.*")
                    (< metric rate-limit))
          (rollup 3 120
            (rate-email "kimmo@tiuhti.net")))
        #(info (:service %) (:metric %)))

      (by [:host :service]
        (where (and (= "derive" (:type event))
                    (not (tagged-any? ["rate"] event)))
          (ddt
           (smap #(assoc %
                         :service (str (:service %) " rate")
                         :tags (conj (:tags %) "rate"))
             reinject)))
        (changed :state {:init "ok"}
          (where (consumer-service? event)
            #(info "consumer service" (:service %) "state changed to" (:state %))))))))
